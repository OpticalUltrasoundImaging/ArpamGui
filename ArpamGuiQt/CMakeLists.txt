project(ArpamGuiQt VERSION 1.0.0 LANGUAGES CXX)

set(EXE_NAME ArpamGuiQt)

#find_package(Vulkan REQUIRED)
find_package(Qt6 CONFIG REQUIRED COMPONENTS Widgets Gui PrintSupport)
qt_standard_project_setup()
set(CMAKE_AUTOMOC ON)

# Manually include QCustomPlot
find_path(QCUSTOMPLOT_INCLUDE_DIR qcustomplot.h PATH_SUFFIXES qcustomplot)
if (WIN32)
    find_library(QCUSTOMPLOT_LIBRARY NAMES qcustomplot2)
    find_library(QCUSTOMPLOT_LIBRARY_DEBUG NAMES qcustomplotd2)

    set(QCUSTOMPLOT_LIBRARY "$<IF:$<CONFIG:Debug>,${QCUSTOMPLOT_LIBRARY_DEBUG},${QCUSTOMPLOT_LIBRARY}>")
else ()
    find_library(QCUSTOMPLOT_LIBRARY NAMES qcustomplot)
endif()

if (QCUSTOMPLOT_INCLUDE_DIR AND QCUSTOMPLOT_LIBRARY)
    message(STATUS "Found QCustomPlot: ${QCUSTOMPLOT_LIBRARY}")
else()
    message(FATAL_ERROR "Could not find QCustomPlot")
endif()

### Icons
# https://doc.qt.io/qt-6/appicon.html
# The MACOSX_BUNDLE_ICON_FILE variable is added to the Info.plist
# generated by CMake. This variable contains the .icns file name,
# without the path.
set(MACOSX_BUNDLE_ICON_FILE ArpamGui.icns)
set(app_icon_macos "${CMAKE_CURRENT_SOURCE_DIR}/resources/icons/ArpamGui.icns")
set_source_files_properties(${app_icon_macos} PROPERTIES 
    MACOSX_PACKAGE_LOCATION "Resources")

set(app_icon_resource_windows "${CMAKE_CURRENT_SOURCE_DIR}/resources/icons/ArpamGui.rc")

### Embed some compile time information as macros
# Current date and time
string(TIMESTAMP CURRENT_DATE "%Y-%m-%d" UTC)
string(TIMESTAMP CURRENT_TIME "%H:%M:%S" UTC)
add_definitions(-DARPAM_COMPILE_DATE="${CURRENT_DATE}")
add_definitions(-DARPAM_COMPILE_TIME="${CURRENT_TIME}")

# Compiler name and version
add_definitions(-DARPAM_COMPILER_NAME="${CMAKE_CXX_COMPILER_ID}")
add_definitions(-DARPAM_COMPILER_VERSION="${CMAKE_CXX_COMPILER_VERSION}")

# Use QCustomPlot as a shared library
add_definitions(-DQCUSTOMPLOT_USE_LIBRARY)

### Source files
qt_add_executable(${EXE_NAME}
    src/main.cpp
    src/MainWindow.hpp
    src/MainWindow.cpp
    src/About.hpp
    src/About.cpp
    src/Canvas.hpp
    src/Canvas.cpp
    src/CustomPlot.hpp
    src/CustomPlot.cpp
    src/AScanPlot.hpp
    src/AScanPlot.cpp
    src/Annotation/Annotation.hpp
    src/Annotation/Annotation.cpp
    src/Annotation/AnnotationModel.hpp
    src/Annotation/AnnotationModel.cpp
    src/Annotation/AnnotationJsonFile.hpp
    src/Annotation/AnnotationJsonFile.cpp
    src/Annotation/AnnotationView.hpp
    src/Annotation/AnnotationView.cpp
    src/Annotation/GraphicsItemBase.cpp
    src/Annotation/GraphicsItemBase.hpp
    src/Annotation/GraphicsItems.cpp
    src/Annotation/GraphicsItems.hpp
    src/CanvasCursorState.hpp
    src/CanvasTicks.hpp
    src/CanvasTicks.cpp
    src/CanvasOverlay.hpp
    src/CanvasOverlay.cpp
    src/CoregDisplay.hpp
    src/CoregDisplay.cpp
    src/ReconParamsController.hpp
    src/ReconParamsController.cpp
    src/FrameController.hpp
    src/FrameController.cpp
    src/Metrics/FWHMTracer.hpp
    src/Metrics/FreqSpectrum.hpp
    src/Metrics/FreqSpectrum.cpp
    src/SaftParamsController.hpp
    src/SaftParamsController.cpp
    src/RFProducerFile.hpp
    src/RFProducerFile.cpp
    src/ReconWorker.hpp
    src/DAQ/DAQ.hpp
    src/DAQ/DAQ.cpp
    src/AcquisitionController.hpp
    src/AcquisitionController.cpp
    src/Motor/NI.hpp
    src/Motor/NI.cpp
    src/CollapsibleGroupBox.hpp
    ${app_icon_macos}
    ${app_icon_resource_windows}
)
target_sources(${EXE_NAME} PRIVATE
    src/Recon.hpp
    src/Recon.cpp
    src/geometryUtils.cpp
    src/geometryUtils.hpp
    src/jsonUtils.cpp
    src/jsonUtils.hpp
)
qt_add_resources(${EXE_NAME} imageresources
    PREFIX "/"
    FILES 
    resources/images/radial_380.png
)
qt_add_resources(${EXE_NAME} styles
    PREFIX "/"
    FILES
    resources/styles/styles.qss
)

target_include_directories(${EXE_NAME} PRIVATE 
    src
    ${QCUSTOMPLOT_INCLUDE_DIR}
)

find_package(fmt CONFIG REQUIRED)
find_package(FFTW3 CONFIG REQUIRED)
find_package(FFTW3f CONFIG REQUIRED)
find_package(KFR CONFIG REQUIRED)

target_link_libraries(${EXE_NAME} PRIVATE
    libuspam
    fmt::fmt
    Qt::Widgets
    Qt::Gui
    Qt::PrintSupport
    ${QCUSTOMPLOT_LIBRARY}
    FFTW3::fftw3
    FFTW3::fftw3f
    kfr
    kfr_dsp
    #Vulkan::Vulkan
)

if(APPLE)  
    # Assume M1 mac
    target_link_libraries(${EXE_NAME} PRIVATE
        kfr_dsp_neon64
    )
endif()

set_target_properties(${EXE_NAME} PROPERTIES
    CXX_STANDARD 20
    CXX_EXTENSIONS OFF
    # INTERPROCEDURAL_OPTIMIZATION TRUE
)


### Configure AlazarTech ATS-SDK on supported platforms
if (WIN32)

    # Check if ATS-SDK is installed
    set(ATS_C_SDK_ROOT "C:/AlazarTech/ATS-SDK/7.7.0/Samples_C")
    set(ATS_SDK_INCLUDE_DIR "${ATS_C_SDK_ROOT}/Include")
    set(ATS_SDK_LIB "${ATS_C_SDK_ROOT}/Library/x64/ATSApi.lib")

    if (EXISTS ${ATS_SDK_LIB})
        message(STATUS "Found ATS-SDK at ${ATS_C_SDK_ROOT}. ArpamGuiQt will build with AlazarTech DAQ support.")

        target_compile_definitions(${EXE_NAME} PRIVATE ARPAM_HAS_ALAZAR)
        target_include_directories(${EXE_NAME} PRIVATE ${ATS_SDK_INCLUDE_DIR})
        target_link_libraries(${EXE_NAME} PRIVATE ${ATS_SDK_LIB})
    else()
        message(WARNING "ATS-SDK not found at ${ATS_C_SDK_ROOT}. Please check your ATS-SDK installation.")
    endif()

endif()


### Configure NI-DAQmx C library on supported platforms
# https://forums.ni.com/t5/Multifunction-DAQ/64bit-NIDAQmx-ANSI-C-library-location/td-p/4319040
if (WIN32)
    set(NI_DAQMX_C_SDK_ROOT "C:/Program Files (x86)/National Instruments/Shared/ExternalCompilerSupport/C")
    set(NI_DAQMX_INCLUDE_DIR "${NI_DAQMX_C_SDK_ROOT}/include")
    set(NI_DAQMX_LIB_DIR "${NI_DAQMX_C_SDK_ROOT}/lib64/msvc")

    if (EXISTS ${NI_DAQMX_C_SDK_ROOT})
        message(STATUS "Found NI-DAQmx 64-bit C SDK at ${NI_DAQMX_C_SDK_ROOT}")
        message(STATUS "ArpamGuiQt will build with NI support")
        message(STATUS "NI_DAQMX_INCLUDE_DIR ${NI_DAQMX_INCLUDE_DIR}")

        set(NI_DAQMX_LIBS 
            ${NI_DAQMX_LIB_DIR}/NIDAQmx.lib
            ${NI_DAQMX_LIB_DIR}/nip2p.lib
            ${NI_DAQMX_LIB_DIR}/nisyscfg.lib
            ${NI_DAQMX_LIB_DIR}/nivision.lib
        )

        target_compile_definitions(${EXE_NAME} PRIVATE ARPAM_HAS_NI)
        target_include_directories(${EXE_NAME} PRIVATE ${NI_DAQMX_INCLUDE_DIR})
        target_link_libraries(${EXE_NAME} PRIVATE ${NI_DAQMX_LIBS})

    else()
        message(WARNING "NI-DAQmx 64-bit C SDK not found at ${NI_DAQMX_C_SDK_ROOT}.")
    endif()

endif()


### Run Qt deploy script
if (WIN32)
    add_compile_definitions(_USE_MATH_DEFINES)

    # On Windows, call windeployqt create a deployable folder
    # https://doc.qt.io/qt-6/windows-deployment.html
    set_target_properties(${EXE_NAME} PROPERTIES
        WIN32_EXECUTABLE ON
    )

    # Determine the path to windeployqt
    set(WINDEPLOYQT_PATH "${Qt6_DIR}/../../tools/Qt6/bin")
    set(WINDEPLOYQT_EXE "${WINDEPLOYQT_PATH}/windeployqt.exe")
    set(WINDEPLOYQT_DEBUG_EXE "${WINDEPLOYQT_PATH}/windeployqt.debug.bat")

    # Add post build command to call windeployqt
    add_custom_command(TARGET ${EXE_NAME} POST_BUILD
        COMMAND "$<IF:$<CONFIG:Debug>,${WINDEPLOYQT_DEBUG_EXE},${WINDEPLOYQT_EXE}>"
        --verbose 1
        "$<TARGET_FILE:${EXE_NAME}>"
    )

elseif (APPLE)


    # Post-build steps for macOS
    # https://doc.qt.io/qt-6/macos-deployment.html

    set_target_properties(${EXE_NAME} PROPERTIES
        MACOSX_BUNDLE ON
        MACOSX_BUNDLE_INFO_PLIST cmake/MacOSXBundleInfo.plist.in
    )

    # Find macdeployqt
    get_target_property(QT_BIN_DIR Qt::qmake IMPORTED_LOCATION)
    find_program(MACDEPLOYQT NAMES macdeployqt HINTS "${QT_BIN_DIR}")

    # Use macdeployqt to bundle Qt libraries
    add_custom_command(TARGET ${EXE_NAME} POST_BUILD
        COMMAND ${MACDEPLOYQT}
        "$<TARGET_BUNDLE_DIR:${EXE_NAME}>"
        -verbose=1
        -always-overwrite
        -codesign=-
        -no-strip
        "$<IF:$<CONFIG:Release>,-dmg,-use-debug-libs>"
        COMMENT "Bundling Qt libraries"
    )

endif()
